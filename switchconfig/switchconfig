#!/usr/bin/perl
# vim: shiftwidth=4 tabstop=4
#
# Copyright by Marco d'Itri <md@linux.it>.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.

use v5.36;
use experimental qw(for_list);

use Pod::Usage;
use Net::Netrc;
use HTTP::Tiny;
use Path::Tiny;
use Template;
use JSON::XS;

# Use a different template for these devices.
my %Switch_Vendor = (
	'sw-mynet'		=> 'Huawei',
	'swc-seeweb'	=> 'Huawei',
);

# This is the IXP Manager API endpoint. Add to your ~/.netrc a section like:
#
# machine https://manager.minap.it/api/
#         password write-your-api-key-here
#
my $IXP_Manager_API = 'https://manager.minap.it/api/';

##############################################################################
{
	my ($action, $switch, $port) = @ARGV;
	usage(1) if not $action;

	my %params;
	if ($action =~ /^(?:-h|--help|help)$/) {
        pod2usage(-verbose => 1, -exitstatus => 0);
    } elsif ($action =~ /^(?:--man|man)$/) {
		$ENV{PERLDOC_PAGER} = 'less -R'
			unless $ENV{PAGER} and $ENV{PAGER} ne 'less';
        pod2usage(-verbose => 2, -exitstatus => 0);
	} elsif ($action eq 'config') {
		usage(1) if not $switch or not $port;
	} elsif ($action eq 'cull-bgp') {
		usage(1) if not $switch;
	} elsif ($action eq 'cull-bgp-off') {
		usage(1) if not $switch;
		$params{culling} = 'off';
		$action = 'cull-bgp';
	} else {
		die "Unknown action '$action'!\n";
	}

	die "Invalid switch name '$switch'!\n" if $switch !~ /^swc?-[a-z]+[0-9]?$/;

	my $cache;
	$cache = path("cache-$switch.json") if $ENV{USER} and $ENV{USER} eq 'md';
	my $l2 = switch_data($switch, $cache);
	die "Not interfaces found on $switch"
		if not @{ $l2->{layer2interfaces} } > 0;
	#use DDP; p $l2; exit;

	$params{switch} = $switch;
	$params{switch_vendor} = lc($Switch_Vendor{$switch} || 'ios');

	if ($action eq 'config') {
		switchconfig($l2, $port, \%params);
	} elsif ($action eq 'cull-bgp') {
		cull_bgp($l2, $port, \%params);
	}

	exit;
}

sub usage ($rc = 0) {
	pod2usage(
		-verbose => 0,
		-exitstatus => $rc,
		-output => ($rc == 0 ? \*STDOUT : \*STDERR),
	);
}

##############################################################################
sub switchconfig ($l2, $port, $params = {}) {
	my $ports = find_ports($l2, $port);
	die "Port '$port' not found on $params->{switch}!\n" if not @$ports;
	#use DDP; p $ports;

	my $template_file = "minap-l2-$params->{switch_vendor}.tt2";
	my $template = path($template_file)->slurp_utf8;
	build_config(\$template, {
		ports => $ports,
		%$params,
	});
	return;
}

sub cull_bgp ($l2, $port, $params = {}) {
	my $ports = find_ports($l2, $port, { type => 'edge' });
	die "Port '$port' not found on $params->{switch}!\n" if not @$ports;
	#use DDP; p $ports;

	my $template_file = 'minap-cull.tt2';
	my $template = path($template_file)->slurp_utf8;
	build_config(\$template, {
		ports => $ports,
		%$params,
	});
	return;
}

##############################################################################
sub switch_data ($switch, $cache, $ttl = 2 * 60 * 60) {
	if ($cache and $cache->exists) {
		my $age = time - $cache->stat->mtime;
		return decode_json($cache->slurp_utf8) if $age < $ttl;
	}

	my $json = ixpmanager_api("v4/provisioner/layer2interfaces/switch-name/$switch.json");

	$cache->spew_utf8($json) if $cache;
	return decode_json($json);
}

sub ixpmanager_api ($api) {
	my $url = $IXP_Manager_API . $api;

	my $http = HTTP::Tiny->new(verify_SSL => 1);
	my $response = $http->get($url, {
		headers => {
			'X-IXP-Manager-API-Key' => ixp_manager_api_key($IXP_Manager_API),
		},
	});
	$response->{content} =~ s/\n$// if $response->{status} == 599;
	die "Could not download $url: "
			. ($response->{status} == 599 ? $response->{content}
			: "$response->{status} $response->{reason}") . "!\n"
		if not $response->{success};

	return $response->{content};
}

sub find_ports ($data, $search, $search_params = {}) {
	my @ports;
	foreach my $port (@{ $data->{layer2interfaces} }) {
		foreach my ($search_param, $search_value) (%$search_params) {
			next if defined $port->{$search_param}
				and $port->{$search_param} ne $search_value;
		}
		push(@ports, $port)
			if  not defined $search or
				($search =~ /^\d+(?:\/[0-4])?$/
					and $port->{name} =~ /^(?:Te1\/0\/|XGigabitEthernet0\/0\/|Ethernet)$search$/) or
				($search =~ /^Et(?:hernet)?(\d+(?:\/[1-4])?)$/
					and $port->{name} =~ /^Ethernet${1}$/) or
				($search =~ /^Te(?:nGigabitEthernet)?1\/0\/\d+$/
					and $port->{name} =~ /^Te1\/0\/$search$/) or
				($search =~ /^Po(?:rt-channel)?(\d+)$/
					and $port->{name} =~ /Port-channel${1}$/) or
				$port->{description} =~ /\Q$search\E/i;
	}

	return \@ports;
}

##############################################################################
sub build_config ($text, $vars) {
	my $template = Template->new(
		POST_CHOMP => 1,
		DEBUG_UNDEF => 1,
		ENCODING => 'utf8',
	) or die "Template->new: $Template::ERROR";

	$template->process($text, $vars) or die $template->error;

	return;
}

sub ixp_manager_api_key ($url) {
	my $machine = Net::Netrc->lookup($url);
	die "No password found in .netrc for $url!\n"
		if not $machine;
	return $machine->password;
}

##############################################################################
__END__

=encoding utf-8

=head1 NAME

switchconfig - generates configuration fragments from IXP Manager data

=head1 SYNOPSIS

switchconfig ACTION config [PORT_NUMBER|MEMBER_NAME]

switchconfig ACTION [cull-bgp|cull-bgp-off] [PORT_NUMBER|MEMBER_NAME]

=head1 DESCRIPTION

This program generates the configuration for a customer port using data
provided by the IXP Manager API.

The port can be specified by:

=over 2

=item * full name (e.g. I<TenGigabitEthernet1/0/1>),

=item * short name (e.g. I<Te1/0/1>),

=item * port number (e.g. I<1>), or

=item * member name (e.g. I<Seeweb>).

=back

If no port is specified then the configuration is generated for all ports
of that switch.

=head1 OPTIONS

Valid actions are:

=over 8

=item B<config>

Generates the configuration for a member port.

=item B<cull-bgp>

Generates the configuration to enable BGP sessions culling
(L<BCP 214|https://www.rfc-editor.org/rfc/rfc8327.html>) for maintenance.

=item B<cull-bgp-off>

Generates the configuration to disable BGP sessions culling.

=item B<--help>

Prints a short help text.

=item B<--man>

Prints the man page.

=back

=head1 AUTHOR

The program and this man page have been written by Marco d'Itri
L<md@linux.it>.

=cut

